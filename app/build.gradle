plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
}

android {
    namespace 'com.hpcompose.ard'
    compileSdk 33

    signingConfigs {
        productionConfig {
            keyAlias 'keyAlias'
            keyPassword 'password'
            storeFile file('../upm-prod.keystore')
            storePassword 'password'
        }
        stagingConfig {
            keyAlias 'keyAlias'
            keyPassword 'password'
            storeFile file('../upm-stag.keystore')
            storePassword 'password'
        }
    }

    defaultConfig {
        minSdk 24
        targetSdk 33

        testInstrumentationRunner "com.hpcompose.ard.HiltTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    flavorDimensions "app"
    productFlavors {
        def PROD_APP_ID = "com.hpcompose.ard"
        def PROD_API_URL = "\"https://api.ard.com/\""
        def PROD_VERSION_CODE = 1
        def PROD_VERSION_NAME = "1.0"

        def STAG_APP_ID = "com.hpcompose.ard.stag"
        def STAG_API_URL = "\"https://staging-api.ard.com/\""
        def STAG_VERSION_CODE = 1
        def STAG_VERSION_NAME = "1.0"

        def DEV_APP_ID = "com.hpcompose.ard.dev"
        def DEV_API_URL = "\"https://api.stackexchange.com/2.3/\""
        def DEV_VERSION_CODE = 1
        def DEV_VERSION_NAME = "1.0"

        prod {
            dimension "app"
            signingConfig signingConfigs.productionConfig
            applicationId PROD_APP_ID
            versionCode PROD_VERSION_CODE
            versionName PROD_VERSION_NAME
            buildConfigField "String", "API_URL", PROD_API_URL
            buildConfigField "Boolean", "NEED_SSL", "false"
            resValue "string", "flavor_app_name", "ARD"
        }
        stag {
            dimension "app"
            signingConfig signingConfigs.stagingConfig
            applicationId STAG_APP_ID
            versionCode STAG_VERSION_CODE
            versionName STAG_VERSION_NAME
            buildConfigField "String", "API_URL", STAG_API_URL
            buildConfigField "Boolean", "NEED_SSL", "false"
            resValue "string", "flavor_app_name", "ARD STAG"
        }
        dev {
            dimension "app"
            applicationId DEV_APP_ID
            versionCode DEV_VERSION_CODE
            versionName DEV_VERSION_NAME
            buildConfigField "String", "API_URL", DEV_API_URL
            buildConfigField "Boolean", "NEED_SSL", "false"
            resValue "string", "flavor_app_name", "ARD DEV"
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.9.0'
    coreLibraryDesugaring("com.android.tools:desugar_jdk_libs:2.0.2")

    // Jetpack Compose
    implementation "androidx.activity:activity-compose:1.6.1"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.runtime:runtime-livedata:1.3.3"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.paging:paging-compose:1.0.0-alpha18"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"

    // Material Design
    implementation "androidx.compose.material3:material3:1.0.1"
    implementation "androidx.compose.material:material:1.3.1"
    implementation "androidx.compose.material:material-icons-extended:1.3.1"

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    def room_version = "2.5.0"
    implementation("androidx.room:room-runtime:$room_version")
    kapt("androidx.room:room-compiler:$room_version")
    // Kotlin Extensions and Coroutines support for Room
    implementation("androidx.room:room-ktx:$room_version")

    // WorkManager with Coroutines
    implementation "androidx.work:work-runtime-ktx:2.8.0"

    // coroutines
    def coroutines_version = "1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // okhttp
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:okhttp-tls")
    implementation("com.squareup.okhttp3:logging-interceptor")

    // retrofit
    def retrofit = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.google.code.gson:gson:2.9.1"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit"

    // hilt
    implementation("com.google.dagger:hilt-android:2.44.2")
    kapt("com.google.dagger:hilt-android-compiler:2.44.2")
    testImplementation("com.google.dagger:hilt-android-testing:2.44.2")
    kaptTest("com.google.dagger:hilt-android-compiler:2.44.2")
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.44.2")
    kaptAndroidTest("com.google.dagger:hilt-android-compiler:2.44.2")

    // glide
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    kapt 'com.github.bumptech.glide:compiler:4.14.2'

    // accompanist
    implementation "com.google.accompanist:accompanist-permissions:0.29.1-alpha"
    implementation ("com.google.accompanist:accompanist-navigation-animation:0.29.1-alpha")
    implementation ("com.google.accompanist:accompanist-pager:0.19.0")

    // firebase
    implementation platform('com.google.firebase:firebase-bom:31.2.3')
    implementation 'com.google.firebase:firebase-analytics-ktx:21.2.0'
    implementation 'com.google.firebase:firebase-messaging-ktx:23.1.2'
}

// Allow references to generated code
kapt {
    correctErrorTypes = true
}